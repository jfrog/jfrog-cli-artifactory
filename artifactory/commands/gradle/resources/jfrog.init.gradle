import org.gradle.util.GradleVersion

def artifactoryUrl = '{{ .ArtifactoryURL }}'
def gradleRepoName = '{{ .GradleRepoName }}'
def artifactoryUsername = '{{ .ArtifactoryUsername }}'
def artifactoryAccessToken = '{{ .ArtifactoryAccessToken }}'
def gradleVersion = GradleVersion.current()
def allowInsecure = gradleVersion >= GradleVersion.version("6.2") && artifactoryUrl.startsWith("http://")

void configureMavenRepo(repositories, String rtUrl, String rtUser, String rtPass, boolean allowInsecure) {
    repositories.maven {
        name = "Artifactory"
        url uri(rtUrl)
        credentials {
            username = rtUser
            password = rtPass
        }
        // This is used when Artifactory is running in HTTP mode
        if (allowInsecure) {
            allowInsecureProtocol = true
        }
    }
}

// Configure the pluginManagement repositories
gradle.settingsEvaluated { settings ->
    settings.pluginManagement {
        repositories {
            configureMavenRepo(it, "${artifactoryUrl}/${gradleRepoName}", artifactoryUsername, artifactoryAccessToken, allowInsecure)
            gradlePluginPortal() // Fallback to Gradle Plugin Portal
        }
    }
}

// Configure the project repositories
allprojects { project ->
    project.repositories {
        configureMavenRepo(it, "${artifactoryUrl}/${gradleRepoName}", artifactoryUsername, artifactoryAccessToken, allowInsecure)
    }
    
    // Configure publishing for projects that apply maven-publish plugin
    project.plugins.withId('maven-publish') {
        project.publishing {
            repositories {
                // Clear any existing repositories to ensure Artifactory is the only publishing destination
                clear()
                maven {
                    name = "Artifactory"
                    url = uri("${artifactoryUrl}/${gradleRepoName}")
                    credentials {
                        username = artifactoryUsername
                        password = artifactoryAccessToken
                    }
                    // This is used when Artifactory is running in HTTP mode
                    if (allowInsecure) {
                        allowInsecureProtocol = true
                    }
                }
            }
        }
    }
}